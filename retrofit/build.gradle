plugins {
  id 'java'
  id("net.ltgt.errorprone") version "0.6"
}

apply plugin: 'java-library'
apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'com.vanniktech.maven.publish'


repositories {
  mavenLocal()
  mavenCentral()
}


dependencies {
  api deps.okhttp

  compileOnly deps.android
  compileOnly deps.kotlinStdLib
  compileOnly deps.kotlinCoroutines

  compileOnly deps.animalSnifferAnnotations
  compileOnly deps.findBugsAnnotations

  testImplementation project(':retrofit:test-helpers')
  testImplementation deps.junit
  testImplementation deps.assertj
  testImplementation deps.guava
  testImplementation deps.mockwebserver
  testImplementation deps.kotlinStdLib
  testImplementation deps.kotlinCoroutines

  annotationProcessor "edu.ucr.cs.riple:nullaway:0.7.12-SNAPSHOT"

  compileOnly "com.google.code.findbugs:jsr305:3.0.2"
  errorprone "com.google.errorprone:error_prone_core:2.3.2"
  errorproneJavac "com.google.errorprone:javac:9+181-r4173-1"
}

tasks.withType(JavaCompile) {
  // remove the if condition if you want to run NullAway on test code
  if (!name.toLowerCase().contains("test")) {
    options.errorprone.disableWarningsInGeneratedCode = true
    options.errorprone.errorproneArgs += ["-XepDisableAllChecks",
                                          "-Xep:NullAway:ERROR",
                                          "-XepOpt:NullAway:AnnotatedPackages=retrofit2",
                                          "-XepOpt:NullAway:AutoFix=true",
                                          "-XepDisableAllWarnings"]
    options.compilerArgs << "-Xmaxerrs" << "100000"
  }
}

jar {
  manifest {
    attributes  'Automatic-Module-Name': 'retrofit2'
  }
}
